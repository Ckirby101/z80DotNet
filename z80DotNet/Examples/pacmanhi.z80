;;-----------------------------------------------------------------------
;; 
;; Pac-Man Hi-Score/Save Enhancement Kit
;;
;; (c) 2017 informedcitizenry
;;
;; This enhancement provides a high-score table with name registration
;; capability. In addition, it provides support for a "save" state 
;; functionality (e.g., non-volatile RAM or volatile RAM with battery 
;; backup) as well as running the attract/demo screen in free play mode.
;;
;;-----------------------------------------------------------------------

    ;; color constants
            BLACK       = 0
            RED         = 1
            PINK        = 3
            CYAN        = 5
            GOLD        = 7
            YELLOW      = 9
            PEACH       = 14
            WHITE       = 15
            BLUE        = 16

            P1_SCR_VRAM = $43f7
            HIGH_SCORE  = $4e88
            
            SCORETABLE_SIZ  = defaulthi_end - defaulthi
            
    ;; patch $0401 from ld a,($4e6e) to freeplystrt
    ;; patch $06be from la a,($4e04) to call checkreg
    ;; patch $0936 & $0955 from rst $28/.db $1c,$05 to call reghiscor
    ;; patch $0973 from ld ($4e02),a to call starthigh
    ;; patch $23ee from rst $20/$f3,$23/$00/$24 to call drawhigh/nop/nop
    ;; patch $24d7 from ld e,b/ld a,b/cp $02 to jp colorscren/.db $02
    ;; patch $2ae2 from call $2c5e to call $8000
    
    
            * = $8000
            
            ld  a,($4e00)
            and a               ; is a == 0?
            jp  nz,$2c5e        ; if not in init state jump to routine
            call $2c5e          ; else call $2ae2 (print "HIGH
                                ; SCORE")
                                
            ;; this will run on bootup during the initialization mode
            ;; of the game
            ld  hl,initflag     ; hl = high score var init flag
            ld a,(hl)           ; has the high score data already been
                                ; initialized? (is this the first time
                                ; machine has been powered up with
                                ; the hi-score kit? basically, this
                                ; flag is useful if the hi-score kit
                                ; had a score "save" capability)
            and a               ; is flag set?
            jr nz,sethiscor     ; no print the high score saved
            inc (hl)            ; else set the flag and initialize
            ld  hl,defaulthi    ; get default high score data
            ld  de,scores
            ld  bc,SCORETABLE_SIZ
            ldir                ; copy default scores to scores
            ld hl,$7650         ; high score
            ld (highscore),hl   ; save in the saved high score
            ld  a,1
            ld  (initflag),a    ; set the init flag
sethiscor:
            ld  hl,highscore    ; restore high score
            ld  de,HIGH_SCORE   ; high score variable destination
            ld  bc,$0003        ; 3 bytes
            ldir                ; copy
            dec de              ; de = high score MSB
            ld  hl,$43f2        ; hl = high score screen RAM highest dig
            ld  bc,$0304        ; b = 3, c = 4
            call $2abe          ; print high score
clrhivars:
            xor a
            ld b,levelstate-remaintick
            ld hl,remaintick
clrhiloop:
            ld (hl),a
            inc hl
            djnz clrhiloop
            ret
            
            ;; DE = player's score, HL = current high score table 
            
reghiscor:
            ld a,($4e00)
            cp $01              ; is a in attract mode?
            jr z,finished       ; yes exit
            ld hl,P1_SCR_VRAM+5 ; player one highest digit
            ld a,($4e09)        ; get current player
            and a               ; is it player 1?
            jr z,+              ; yes skip ahead
            ld l,$e9            ; else player two highest digit
+           ld (currscor),hl    ; save in player score
            ld c,$00            ; for c = 0 to 4
         ;; highest digit of highest score
            ld iy,firstscore.score+5 
nextscor:
            push iy             ; 
            pop hl              ; next score entry
            ld de,(currscor)    ; get current player score             
            ld b,$06            ; for b = 1 to 6
nextdigit:
            ld a,(hl)           ; compare highest digits
            and $0f             ; only get low nybble (blank space = 0)
            ld ixl,a            ; save in ixl
            ld a,(de)           ; get comparison
            and $0f             ; only get low nybble
            cp ixl              ; 
            jr c,playrless      ; player's score less, next score
            jr nz,regplayer     ; player's score greater, register
            dec de              ; next lowest digit in player score
            dec hl              ; next lowest digit in high score
            djnz nextdigit      ; next digit
            jr regplayer        ; we looked at all digits and player's
                                ; score is greater or equal
playrless:
            inc c
            ld a,c              ; c -> a
            cp $05              ; A == 5?
            jr z,finished       ; looked at all score, player did not
                                ; get a high score
            ld  de,secondscore.score-firstscore.score
            add iy,de           ; iy = next score lowest digit
            jr  nextscor        ; next score
finished:   
            rst $28             ; insert task
            .byte $1c           ; draw text
            .byte $05           ; "GAME OVER"
           
            ret                 ; return

regplayer:
            ld a,c              ; c -> a (high score index)
            ld e,c              ; c -> e
            ld (currhighix),a   ; save in current high index
            rlc e               ; e *= 2
            ld d,0              ; d = 0
            ld hl,$40c8         ; highest initial
            add hl,de           ; 
            ld (currinit),hl    ; save hl in currinit pointer
            cp  4               ; a >= 4? if so no need to transfer
            jr nc,sethigh       
            ld  a,4             ; else a = 4 - a
            sub c
            call times12        ; get a*12 => bc (current high score + 1)
            ;; last byte of second lowest score => hl
            ld hl,fourthscore.name+2        
            ;; last byte of lowest score => de
            ld de,lastscore.name+2
            lddr                ; (de--) = (hl--) bc times
            ld a,(currhighix)
sethigh:    call times12
            ld ix,scores
            add ix,bc
            ld (ix+0),0         ; reset the top two
            ld (ix+1),0         ; level vars
            ld a,($4e13)        ; get current level in a
            inc a               ; a = a + 1
sub100:
            ld b,a              ; save in b
            sub 100             ; subtract 100
            jr c,tens           ; less than 100 get tens
            inc (ix+0)          ; increment
            jr sub100           ; and go back
tens:
            ld a,b              ; get b back in a
sub10:
            ld b,a
            sub 10
            jr c,remain
            inc (ix+1)
            jr sub10
remain:
            ld a,b              ; get a back from b
            ld (ix+2),a         ; store last digit 
            ld a,(ix+0)         ; is highest digit of level zero?
            and a
            jr nz,setscor       ; no then set score, else...
            ld (ix+0),'@'       ; blank out zero
            ld a,(ix+1)         ; is second digit zero?
            and a
            jr nz,setscor       ; no set score
            ld (ix+1),'@'       ; else blank out
setscor:
            ld bc,8             ; advance ix 8 places to highest digit of score
            add ix,bc           
            push ix
            pop de              ; transfer to DE
            ld hl,(currscor)    ; hl = player score in vram
            dec bc
            dec bc              ; copy 6 bytes
            lddr
            ld  a,'A'           ; initialize the name
            ld (ix+1),a         ; first initial is 'A'
            dec a
            ld (ix+2),a         ; rub out the other two
            ld (ix+3),a
            call setlevel       ; set the current level         
                                ; at this state of the game)
            ld a,($4e09)        ; get current player
            ld (currplayr),a    ; save current player
            ld a,$3c
            ld (remaintick),a   ; set remaining ticks to 60
            jp finished         ; and go back to normal festivities
                        
drawhigh:
            rst $20
            .word $23f3         ; set parts of screen to spaces
            .word $2400         ; or set entire screen to spaces
            .word printhis      ; or print the high score table
            .word printhisr     ; print high score table for registration

printhis:   
            call $2400          ; clear screen
            ld  hl,scores
            ld  de,rows
            ld  (score_ptr),hl
            ld  (row_ptr),de
            ld  hl,tableheader
            call $2c62          ; print the table header
            ld  c,5
printhiloop:     
            ld  iy,(row_ptr)
            ld  e,(iy+0)        ; read (iy) -> de
            ld  d,(iy+1)
            push de
            pop ix              ; move to ix
            ld  a,(iy+2)
            ld  (ix+0),a        ; first draw the rank numbers
            ld  a,(iy+3)
            ld  (ix-$20),a
            ld  a,(iy+4)
            ld  (ix-$40),a
            ld  iy,(score_ptr)
            ld  de,-192         ; offset to next data column
            add ix,de           ; now ix is start of current level
            ld  a,(iy+0)
            ld  (ix+0),a
            ld  a,(iy+1)
            ld  (ix-$20),a
            ld  a,(iy+2)
            ld  (ix-$40),a
            add ix,de
            ld  a,(iy+8)        ; score highest digit
            ld  (ix+$20),a
            ld  a,(iy+7)
            ld  (ix+$00),a
            ld  a,(iy+6)
            ld  (ix-$20),a
            ld  a,(iy+5)
            ld  (ix-$40),a
            ld  a,(iy+4)
            ld  (ix-$60),a
            ld  a,(iy+3)
            ld  (ix-$80),a
            add ix,de
            ld  a,(iy+9)        ; first initial
            ld  (ix-$60),a
            ld  a,(iy+10)
            ld  (ix-$80),a
            ld  de,-160         ; offset to final initial
            add ix,de
            ld  a,(iy+11)
            ld  (ix+0),a        ; last initial
            dec c
            jr  z,done
            ld  de,12
            ld  hl,(score_ptr)
            add hl,de           ; hl := scores
            ld  (score_ptr),hl
            ld  hl,(row_ptr)
            ld  de,5
            add hl,de
            ld (row_ptr),hl
            jp  printhiloop
done:       ret 
            
printhisr:
            call printhis
            ld hl,congrats      ; HL = address of congratulation msg
            call $2c62          ; print "YOU GOT A HIGH SCORE!"
            ld hl,instruct      ; HL = address of instruction msg
            call $2c62          ; print "ENTER YOUR INITIALS"
            ld hl,timeremain    ; HL = address of timeremain
            call $2c62          ; print "TIME REMAINING..."
            ld hl,(currinit)    
            ld de,$0400
            add hl,de           ; hl = color ram of initial
            ld a,YELLOW         ; 
            ld (hl),a           ; set initial color to yellow
            ret     
                        
checkreg:
            ld  a,($4e04)       ; get play state routine
            cp  $26             ; is it < $26?
            jp c,$06c1          ; yes just go back to play
            sbc a,$26           ; subtract $26 for
            rst $20             ; jump table
            
            .word $000c         ; wait for "GAME OVER" screen to finish
            .word reginit       ; initialize registration screen
            .word $000c         ; wait for initialization to finish
            .word doregister    ; do registration
            .word $000c         ; wait after registering
            .word regfinish     ; finalize

            ;; $0973 will call here when patched 
starthigh:
            ld ix,$4e00         ; main routine
            ld (ix+$02),a       ; $4e02 - attract mode, reset for now
            ld b,(ix+$00)       ; get main routine 
            bit 1,b             ; is it attract (%01) or play (%11) mode?
            ret nz              ; if main routine just return, else
            ld c,$24            ; C = hiscorinit
            ld (ix+$02),c       ; set attract
            ret                 ; return to unpatched pac-man
            nop
            
freeplystrt:
            
            ld a,($4e6e)
            and a
            jr z,freerunatr
            cpl
            and a               ; is free play set?
            jp nz,$0407         ; no, do "normal" ready to play screen
            ld a,$01
            ld ($4e03),a        ; set "ready for play" to 1
            call $062c          ; else check if start button(s) pressed
            ld a,($4e03)
            cp $02              ; is a == 2? (start button pressed?)
            jr nz,freerunatr    ; no process free play attract
            ld ($4e00),a        ; else set to ready for play state
            xor a
            ld ($4e04),a
            ld ($4e02),a
            
            rst $28
            .byte $1e           ; task 30
            .byte $00           ; clear all sprites
            
            ret
            
freerunatr:    
            ld a,($4e02)
            cp  $24             ; is attract state less than $24?
            jp  c,$0416         ; yes do "normal" attract
            sbc a,$24           ; else subtract $24 for
            rst $20             ; jump table
            
            .word hiscorinit
            .word $000C         ; wait
            .word hiscorfin
            
colorscren:
            ld e,b
            ld a,b
            cp $02
            jp z,$24db
            jp c,$24df
            ld a,CYAN
            jp $24e1
                       
reginit:
            rst $28
            .byte $00           ; task 0
            .byte $01           ; clear maze
            
            rst $28
            .byte $01           ; task 1
            .byte $02           ; color screen white
            
            rst $28
            .byte $00           ; task 0
            .byte $03           ; show highscore screen for registration
            
            rst $28
            .byte $1e           ; task 30
            .byte $00           ; clear sprites
            
            rst $30
            .byte $41           ; wait 1/10ths of a second
            .byte $00           ; task 0
            .byte $00           ; increment subroutine
            
            ld hl,$4e04
            inc (hl)
            ret
                        
            ;; run registration frame
doregister:
            ld hl,$40db         ; HL = lowest digit of counter
            ld de,$40fb         ; DE = highest digit counter
            ld a,(de)           ; get highest digit of timer
            and a               ; is it zero?
            jr nz,updremtick    ; if not zero update remain ticker
            ld ($44fb),a        ; else black out leading digit
            or (hl)             ; or with lowest digit
            jp z,setfinish      ; if timer zero set finish
updremtick:
            ld a,(remaintick)   ; else get ticks to drive remaining timer
            dec a               ; a == 0?
            jr nz,flashscore    ; no flash score
            dec (hl)            ; decrement
            jp p,resetrem       ; if >= 0 reset reaminder subsecond tickr
            ld a,9
            ld (hl),a           ; else set digit to 9
            ex de,hl            ; hl = de (higher digit)
            dec (hl)            ; decrement next digit
resetrem:
            ld a,60             ; a = ticks per second
flashscore:
            ld (remaintick),a   ; update subsecond ticker
            ld a,(currplayr)    ; current player in A
            ld hl,$43d8         ; "1UP" screen RAM
            and a             ; is it player 1? yes skip
            jr z,checkflash     ; if so check if flashing
            ld hl,$43c5         ; else "2UP" screen RAM
checkflash:
            ld c,BLACK
            ld a,(hl)           ; read the "UP"
            cp '@'            ; is it a blank space
            jr z,doflash        ; yes do color score black
            ld c,WHITE          ; else color score white
doflash:
            ld ix,$4608         ; highest digit of highest score in color ram
            ld a,(currhighix)   ; get current high index
            sla a               ; a = a * 2
            add a,ixl           ; 
            ld ixl,a            ; HL = HL + a * 2
            ld (ix+$00),c       ; switch color for each digit
            ld (ix-$20),c       
            ld (ix-$40),c
            ld (ix-$60),c
            ld (ix-$80),c
            ld de,-$a0
            add ix,de
            ld (ix+$00),c
            ld de,$5000         ; else DE = IN0
            ld a,($4e72)        ; upright or cocktail
            ld c,a              ; save in C
            ld a,(currplayr)    ; get current player
            and c               ; is current player player 1 or upright?
            jr z,inputloop      ; yes, get player input
            ld de,$5040         ; DE = IN1            
inputloop:
            ld hl,(currinit)    ; current initial screen RAM offset
            ld a,(de)           ; get joystick
            ld b,a              ; save in b
            cpl                 ; invert bits (low = input pressed)
            and %00001111       ; check only lowest 4 bits
            jr nz,checklock     ; if joy input check lock
            ld (inputlock),a    ; else reset input lock
            ret
checklock:
            ld a,(inputlock)    ; is input lock set?
            and a               ; 
            ret nz              ; if set return
            ld a,b              ; else get original input back
            bit 0,a             ; joystick up
            jp z,initup         ; advance initial
            bit 1,a             ; joystick left
            jp z,clearini       ; backtrack initial
            bit 3,a             ; joystick down
            jr z,initdown       ; clear initial
            
    ;; joystick right pressed select initial
            push hl
            ld bc,$0400         ; bc = offset to color
            add hl,bc           ; add to current initial
            ld a,WHITE
            ld (hl),a           ; set to white
            pop hl              ; get original back
            ld de,-32           ; advance to next initial
            add hl,de           ; add offset to next char
            ld a,l              ; get LSB in a
            cp $80              ; are we one column past 3rd initial
            jr c,setfinish      ; yes, finish and return
setinit:    
            ld a,'A'
            ld (hl),a           ; store next initial as 'A'
setinitclr:
            ld (currinit),hl    ; save next initial 
            add hl,bc           ; add offset to color ram
            ld a,YELLOW
            ld (hl),a           ; set to YELLOW
setinlock:
            ld a,1
            ld (inputlock),a    ; set inputlock
            ret
                        
setfinish:
            ;; clear instruction/time remaining messages
            ld a,'@'            ; blank space
            ld de,$0020         ; column offset
            ld ix,$4055         ; last/first column of first rows
            ld c,4              ; for c = 1 to 4
rubrow:     
            ld b,28             ; for b = 1 to 28
            push ix
            pop hl
rubchar:    
            ld (hl),a           ; blank space
            add hl,de           ; next column
            djnz rubchar        ; clear until 28
            inc ix
            inc ix              ; next row
            dec c               ; next c
            jr nz,rubrow       

            ld hl,nameregistered
            call $2c62
            
            rst $28             
            .byte $01           ; task 1
            .byte $02           ; color screen white
         
            rst $30             ;  
            .byte $5a           ; and wait 2.6 seconds
            .byte 0             ; task 0
            .byte 0             ; to do final registration tasks
            
            ld hl,$4e04
            inc (hl)            ; increment attract mode routine    
            ret                 ; return
            
initdown:
            ld a,(hl)           ; read initial
            dec a               ; decrease
            cp '@'              ; less than $40? (blank space)
            jr nc,storeleft     ; no just store
            cp '?'              ; is it 1 < space?
            jr z,setspace       ; set space
            ld a,'Z'            ; else less than '.' so make 'Z'
storeleft:
            ld (hl),a           ; else store
            jr setinlock        ; set inputlock
setspace:
            ld a,'@'
            jr storeleft        ; set lowest initial char to space
            
initup:
            ld a,(hl)
            inc a
            cp '&'              ; is it one after '.'?
            jr nz,comparz       ; no, just compare to Z+1 
            ld a,'%'
            jr storeleft        ;
comparz:
            cp 'Z'+1            ; is it >'Z'
            jr c,storeleft      ; no store
            ld a,'%'            ; else set rightmost initial to '.'
            jr storeleft        ; and store
           
clearini:
            ld a,'@'            ; clear the initial
            ld e,l              ; save l in e
            ld (hl),a         
            ld bc,32            ; move initial back one column
            add hl,bc
            ld a,l
            ld bc,$0400         ; now set BC to color offset
            cp $d0
            jp c,setinitclr     ; don't go back too far!
            ld l,e              ; else restore original LSB
            jp setinitclr            
            
regfinish:
            ld hl,HIGH_SCORE    ; save high score (for save kit)
            ld de,highscore
            ld bc,3
            ldir                ; copy high score
            ld a,(currhighix)
            ld c,a
            sla c               ; c = c * 2
            ld ix,$40c8         ; highest initial 
            add ix,bc           ; add offset from c
            call times12        ; now get offset to the name
            ld iy,firstscore.name
            add iy,bc
            ld a,(ix+0)
            ld (iy+0),a
            ld a,(ix-$20)
            ld (iy+1),a
            ld a,(ix-$40)
            ld (iy+2),a
            ld a,(levelstate)   ; get the level state
            inc a               ; increment it
            ld ($4e04),a        ; and set it
            
            rst $28
            .byte $00           ; task 0
            .byte $01           ; clear high score screen
            
            rst $30
            .byte $41           ; 1/10ths of a second
            .byte $00           ; task 0
            .byte $00           ; increment level state
            
            jp clrhivars        ; clear high score vars
            
hiscorinit:
            rst $28
            .byte $00           ; task 0
            .byte $02           ; show highscore screen
            
            rst $28
            .byte $01           ; task 1
            .byte $03           ; color screen aqua blue
            
            rst $28
            .byte $1e           ; task 30
            .byte $00           ; clear sprites
            
            rst $30
            .byte $7f           ; wait 6.3 seconds  
            .byte 2             ; task 2 
            .byte 0             ; increase attract mode routine
            
            ld hl,$4e02
            inc (hl)            ; increment
            ret
                                 
hiscorfin:
            xor a
            ld ($4e02),a        ; reset attract mode routine
            ret
            
setlevel:   
            ld hl,$4e04
            ld a,(hl)           ; get current level state
            ld b,a              ; store original levelstate in b
            ld a,$2b            ; $2b - levelstate
            sub b               ; 
            ld (hl),a           ; will be either $25 or $26 based on
                                ; whether is 6 or 5
            ld a,b              ; get original level back
            and $fe             ; make it either 4 or 6
            ld (levelstate),a   ; and save for later to
            ret
            
times12     ld  b,a
            rlca
            rlca
            rlca                ; a = a * 8
            rlc b               ; 
            rlc b               ; b = b * 4
            add a,b             ; a now = original a * 12
            ld c,a
            ld b,0              ; bc = a
            ret
            
defaulthi:
            .string "@@",1
            .byte 0,5,6,7,'@','@'
            .string "ABC"
            .string "@@",1
            .byte 0,0,1,6,'@','@'
            .string "DEF"
            .string "@@",1
            .byte 0,5,9,5,'@','@'
            .string "GHI"
            .string "@@",1
            .byte 0,5,0,5,'@','@'
            .string "JKL"
            .string "@@",1
            .byte 0,0,3,4,'@','@'
            .string "MNO"
            
defaulthi_end

rows:       
            .word $4368
            .string "1ST"
            .word $436a
            .string "2ND"
            .word $436c
            .string "3RD"
            .word $436e
            .string "4TH"
            .word $4370
            .string "5TH"
            
    ;; format of text strings used in $2c65...
    ;; bytes 0 and 1 : screen/color offset
    ;; bytes 2...n-1 : ascii (sort of) text
    ;; byte n        : terminator ($2f)
    ;; byte n+1      : text color (high bit set = whole text color)
    
tableheader:
            .word $0385
            .string "RANK@@LEVEL@@SCORE@@@NAME"
            .byte $2f
            .byte ($80 | WHITE)
    
congrats:
            .word $0315+$20
            .string "YOU@GOT@A@HIGH@SCORE["
            .byte $2f
            .byte ($80 | PEACH)
            
instruct:
            .word $0317
            .string "ENTER@YOUR@INITIALS"
            .byte $2f
            .byte ($80 | PEACH)
            
timeremain:
            .word $031b
            .string "TIME@REMAINING@;@"
            .byte $05,$00,$2f
            .byte ($80 | CYAN)
            
nameregistered:

            .word $0315+$40
            .string "YOUR@NAME@IS@REGISTERED"
            .byte $2f
            .byte ($80 | WHITE )
            
            .fill $8800-*,$ff

    ;; uninitialized variables
            
            scores     
firstscore  .block
rank        .lint ?
score       .lint ?,?
name        .lint ?
            .endblock
secondscore .block
rank        .lint ?
score       .lint ?,?
name        .lint ?
            .endblock
thirdscore  .block
rank        .lint ?
score       .lint ?,?
name        .lint ?
            .endblock
fourthscore .block
rank        .lint ?
score       .lint ?,?
name        .lint ?
            .endblock
lastscore   .block
rank        .lint ?
score       .lint ?,?
name        .lint ?
            .endblock
 
remaintick  .byte ?
currplayr   .byte ?
currscor    .word ?
currinit    .word ?
score_ptr   .word ?
row_ptr     .word ?
currhighix  .byte ?
inputlock   .byte ?
levelstate  .byte ?
highscore   .long ?
initflag    .byte ?
